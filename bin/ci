#!/usr/bin/env elixir

Mix.install([
  {:spitfire, path: "."},
  :nimble_csv
])

defmodule ParseException do
  defexception [:message]
end

defmodule Main do
  alias NimbleCSV.RFC4180, as: CSV

  @ignore [
    "/lib/**/ebin/**/*",
    "/lib/**/_build/**/*",
    "/lib/**/tmp/**/*"
  ]

  def collect_files(argv) do
    root = List.first(argv)
    files = Path.wildcard(Path.join(root, "lib/**/*.ex"))
    ignore = Enum.flat_map(@ignore, &Path.wildcard(Path.join(root, &1)))
    for file <- files, file not in ignore, {:ok, content} <- [File.read(file)] do
      {file, content}
    end
  end

  def run(files) do
    IO.puts("#{length(files)} files")
    Enum.map(files, fn {file, content} ->
      IO.puts("==> #{file}")

      spitfire = Spitfire.parse(content)
      core = Code.string_to_quoted(content, columns: true, token_metadata: true, emit_warnings: false)
      if spitfire == core do
        :ok
      else
        File.mkdir_p!("tmp")
        File.write!("tmp/spitfire.ex", inspect(spitfire, pretty: true, printable_limit: :infinity, limit: :infinity))
        File.write!("tmp/core.ex", inspect(core, pretty: true, printable_limit: :infinity, limit: :infinity))

        exe = System.find_executable("delta") || "diff"

        {diff, _} = System.cmd(exe, ["tmp/spitfire.ex", "tmp/core.ex"])
        IO.puts file
        IO.puts diff
        raise ParseException, "Failed on file: #{file}"
      end
    end)
  end
end

files = Main.collect_files(System.argv())

{time, _data} = :timer.tc(fn -> Main.run(files) end, :microsecond)

IO.puts("Parsed in #{time}us")
